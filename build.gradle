buildscript {
    ext.kotlin_version = '1.2.20'

    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

plugins {
    id "com.jfrog.bintray" version "1.8.0"
}

apply plugin: 'java'
apply plugin: 'kotlin'

sourceCompatibility = 1.8

group 'org.team401'
version '1.1'


repositories {
    mavenCentral()
    maven {
        url "https://dl.bintray.com/team401/Vision"
    }
}

configurations {
    required //Represents a dependency that must be provided by the applications using the library
    compile.extendsFrom required
}


dependencies {
    commpile "org.jetbrains.kotlin:kotlin-stdlib-jre8:$kotlin_version"

    compile group: 'org.jv4l2', name: 'JV4L2', version: '1.1'

    testCompile group: 'junit', name: 'junit', version: '4.12'
    required fileTree(dir: 'lib', include: '**/*.jar')

    compile 'com.google.code.gson:gson:2.8.1'
}

jar {
    dependsOn configurations.runtime
    from {
        (configurations.runtime - configurations.required).collect {
            it.isDirectory() ? it : zipTree(it)
        }
    }
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

bintray {
    user = project.hasProperty('user') ? project.property('user') : System.getenv("BINTRAY_USER") != null ? System.getenv("BINTRAY_USER") : ""
    key = project.hasProperty('key') ? project.property('key'): System.getenv("BINTRAY_KEY") != null ? System.getenv("BINTRAY_KEY") : ""
    configurations = ['archives']
    pkg {
        repo = "Vision"
        name = "SnakeEyes"
        userOrg = "team401"
        licenses = ["GPL-3.0"]
        vcsUrl = "https://github.com/team401/snakeskin"
        version {
            name = this.version
        }
    }
}